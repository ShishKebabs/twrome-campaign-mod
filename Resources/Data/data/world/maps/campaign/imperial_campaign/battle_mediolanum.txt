;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;; MEDIOLANUM BATTLE
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

		set_battle campaign\imperial_campaign\descr_battle_mediolanum.txt;
		battle_wait 0.2;

		monitor_event ScrollDidOpen ScrollDidOpen conditions_complete_scroll;
			enable_ui conditions_complete_scroll;
			enable_ui conditions_complete_quit_button;
			disable_ui conditions_complete_continue_button;
			remove_battle_map_arrow;
			ui_flash_start conditions_complete_quit_button;
			terminate_monitor;
		end_monitor;

		monitor_event ScrollDidOpen ScrollDidOpen post_battle_scroll;
			enable_ui post_battle_scroll;
			enable_ui post_battle_stats_show_results_button;
			ui_flash_start post_battle_stats_show_results_button;
			advance_advice_thread Victory_1_type_b;													;; Message displayed in Marcus' panel

			terminate_script;
			terminate_monitor;
		end_monitor;

		monitor_event ScrollClosed ScrollClosed end_game_scroll_hd;
			toggle_minimap;
		end_monitor;

		monitor_event UnitInfoOpened TrueCondition;
			enable_ui close_animated_frame_button;
			terminate_monitor;
		end_monitor;

		prepare_for_battle;

		label_location 30, -300 GatePoint;
		label_location 31, -255 GateCentre;
		label_location -35, -256 Breach;
		label_location -15, -170 CavBreach;
		label_location 30, -160 InfBreach;
		label_location 30, 35 TownCentre;
		label_location -30, -220 CounterBreach;
		label_location -30, -365 BetweenArty;
		label_unit 0 0 0 player_general;
		label_unit 0 0 1 player_hastati_1;
		label_unit 0 0 2 player_hastati_2;
		label_unit 0 0 3 player_triarii;
		label_unit 0 0 4 player_archers;
		label_unit 0 0 5 player_mercCav;
		label_unit 0 0 6 player_onagers_1;
		label_unit 1 0 0 barb_chieftain;
		label_unit 1 0 1 barb_infantry_1;
		label_unit 1 0 2 barb_infantry_2;
		label_unit 1 0 3 barb_horse;
		;;unit_set_morale barb_infantry_2 high;

		toggle_minimap;
		hide_ui;
		disable_entire_ui;
		enable_ui advisor_continue;
		disable_shortcuts true;							;;camera shortcuts are not disabled
		disable_shortcuts options_button false;
		disable_shortcuts camera::step_l false;
		disable_shortcuts camera::rot_l_battle false;
		disable_shortcuts camera::step_r false;
		disable_shortcuts camera::rot_r_battle false;
		disable_shortcuts camera::step_fwd false;
		disable_shortcuts camera::step_bck false;
		disable_shortcuts camera::cam_down false;
		disable_shortcuts camera::cam_up false;
		disable_shortcuts camera::rot_u false;
		disable_shortcuts camera::rot_d false;
		inhibit_camera_input true;
		enable_ui campaign_map_pause_button;
		disable_ui advisor_portrait_button;
		enable_ui advisor_continue;
		enable_ui pause_button;
		enable_ui pause_menu_buttons;
		;;enable_ui play_button;
		;;enable_ui ffwd_button;
		;;enable_ui ultra_ffwd_button;
		enable_ui tooltip;
		;;battle_default_camera;
		console_command invulnerable_general "Flavius Julius"
		;;camera_restrictions_set 	off;
		camera_default_mode_set		tw;
		box_drag_selection off;


		block_unit_selection on player_archers;
		block_unit_selection on player_general;
		block_unit_selection on player_triarii;
		block_unit_selection on player_hastati_1;
		block_unit_selection on player_hastati_2;
		block_unit_selection on player_onagers_1;
		block_unit_selection on player_mercCav;

		camera_position 			-340, 350, -400 120, 200, 30;							;; CAMERA START POSITION

		set_camera_bookmark 1, 		45, 320, -390 30, 190, 30;								;; above player army, looking at Mediolanum

		set_camera_bookmark 2,  	10, 310, -290 40, 300, -250;								;; looking at GatePOint

		set_camera_bookmark 3, 		-10, 330, -400 -30, 300, -300;								;; looking at onagers and the settlement

		set_camera_bookmark 4, 		-10, 340, -110 -25, 280, -260;								;; looking at CavBreach

		set_camera_bookmark 5, 		-10, 340, 80 30, 310, 10;									;; looking at TownCentre

		set_camera_bookmark 6, 		-70, 320, -240 20, 300, -300;								;; looking at players army from Mediolanum

		set_camera_bookmark 7, 		-110, 320, -440 10, 290, -280;								;; behind player cav

		set_camera_bookmark 8, 		20, 315, -350 30, 290, -270;								;; behind Triarii

		set_camera_bookmark 9, 		20, 320, -330 -20, 300, -365;								;; looking at Onagers, away from the settlement

		set_camera_bookmark 10, 	-10, 330, -300 -40, 290, -250;								;; zooming to arty breach

		set_camera_bookmark 11, 	10, 330, -300 30, 300, -250;								;; zooming to fighting, looking towards the settlement

		battle_wait 3;

		declare_counter UnitsRouted;														;; Ensuring that enemy units stay routed
		monitor_event UnitHasRouted UnitHasRouted barb chieftain cavalry early gaul;
			unit_set_morale barb_chieftain routing;
			inc_counter UnitsRouted 1;
		end_monitor;
		monitor_event UnitHasRouted UnitHasRouted barb infantry gaul;
			unit_set_morale barb_infantry_1 routing;
			inc_counter UnitsRouted 1;
		end_monitor;
		monitor_event UnitHasRouted UnitHasRouted barb infantry gaul;
			unit_set_morale barb_infantry_2 firm;
			terminate_monitor;
			;;inc_counter UnitsRouted 1;
		end_monitor;
		monitor_event UnitHasRouted UnitHasRouted barb cavalry gaul;
			unit_set_morale barb_horse routing;
			inc_counter UnitsRouted 1;
		end_monitor;
		declare_counter GeneralDead;
		monitor_event BattleGeneralKilled TrueCondition;
			inc_counter GeneralDead 1;
			terminate_monitor;
		end_monitor;

		unit_set_weapon_upgrade player_general 3;
		unit_set_weapon_upgrade player_triarii 3;
		unit_set_weapon_upgrade player_mercCav 3;
		unit_set_weapon_upgrade player_hastati_1 3;
		unit_set_weapon_upgrade player_hastati_2 3;
		unit_set_armour_upgrade player_general 3;
		unit_set_armour_upgrade player_triarii 3;
		unit_set_armour_upgrade player_mercCav 3;
		unit_set_armour_upgrade player_hastati_1 3;
		unit_set_armour_upgrade player_hastati_2 3;
		unit_set_experience player_general 6;
		unit_set_experience player_triarii 6;
		unit_set_experience player_mercCav 6;
		unit_set_experience player_hastati_1 6;
		unit_set_experience player_hastati_2 6;
		unit_set_skirmish_mode barb_horse off;
		unit_set_skirmish_mode player_archers off;
		unit_set_guard_mode barb_infantry_1 on;
		unit_set_guard_mode barb_horse on;
		;;unit_set_fire_at_will_mode player_archers off;
		declare_timer Mediolanum_Pause_Timer;
		filter_unit_commands off change_formation;
		filter_unit_commands off change_move_speed;
		filter_unit_commands off change_formation_width_in_men;
		filter_unit_commands off player_general;
		filter_unit_commands off player_hastati_1;
		filter_unit_commands off player_hastati_2;
		filter_unit_commands off player_triarii;
		filter_unit_commands off player_archers;
		filter_unit_commands off player_onagers_1;
		filter_unit_commands off player_mercCav;
		filter_unit_commands on change_move_speed;

		;; Advisor Introduction
		
		inhibit_camera_input true;
		battle_wait 1;
		camera_zoom_to_bookmark 1;															;; Camera pans to the breach
		battle_wait 1;
		inhibit_camera_input false;
		battle_wait 3;
		show_ui;
		battle_wait 2;
		unit_order_move barb_infantry_1 30, -220;
		advance_advice_thread Introduction_1;												;; Message displayed in Marcus' panel
		while I_AdvisorSpeechPlaying;
		end_while;
		ui_flash_start advisor_continue;												;; Highlighter around Continue button
		while I_AdvisorVisible;																;; Player left-click on the highlighted button
		end_while;
		ui_flash_stop advisor_continue;
		battle_wait 1;																		;; Next advice triggered
		;; Pause

		battle_wait 2;
		;;disable_shortcuts false;
		inhibit_camera_input false;
		unit_order_turn barb_horse 180 absolute;
		unit_order_turn barb_infantry_2 180 absolute;
		unit_order_turn barb_chieftain 180 absolute;
		
		;;enable_entire_ui;
		;;disable_shortcuts order_withdraw true;
		disable_ui order_withdraw true;
		advance_advice_thread Pause_1_type_b;														;; Message displayed in Marcus' panel
		ui_flash_start pause_button;
		;;battle_wait 4;
		enable_ui pause_button;
		declare_counter PauseButtonPressed;
		monitor_event ButtonPressed ButtonPressed pause_button;

		set_counter PauseButtonPressed 1;

			terminate_monitor;
		end_monitor;
		while I_CompareCounter PauseButtonPressed = 0;										;; Player left-click on the highlighted button
		end_while;
		set_counter PauseButtonPressed 0;
		ui_flash_stop pause_button;
		while I_AdvisorSpeechPlaying;														;; Advisor panel is automatically closed
		end_while;
		disable_ui pause_button;
		enable_ui half_speed_button;
		ui_flash_start half_speed_button;
		declare_counter SlowButtonPressed;
		monitor_event ButtonPressed ButtonPressed half_speed_button;

		set_counter SlowButtonPressed 1;

			terminate_monitor;
		end_monitor;
		while I_CompareCounter SlowButtonPressed = 0;										;; Player left-click on the highlighted button
		end_while;
		set_counter SlowButtonPressed 0;
		ui_flash_stop half_speed_button;
		while I_AdvisorSpeechPlaying;														;; Advisor panel is automatically closed
		end_while;
		;;disable_ui pause_button;
		;;disable_ui play_button;
		;;disable_ui ffwd_button;
		;;disable_ui ultra_ffwd_button;
		restart_timer Mediolanum_Pause_Timer;

		while I_TimerElapsed Mediolanum_Pause_Timer < 2000;										;Do nothing for a bit
    	end_while;																				;once the time is up stop the timer



		;; Explain Ram
		inhibit_camera_input true;
		advance_advice_thread Siege_Equipment_Ram_1_type_b; 										;; Message displayed in Marcus' panel
		enable_ui_card unit, 3;
		block_unit_selection off player_triarii;
		;;camera_zoom_to_unit player_triarii;
		inhibit_camera_input true;
		battle_wait 1;
		camera_zoom_to_bookmark 8;															;; Camera pans to player's ram
		battle_wait 1;
		inhibit_camera_input false;												
		point_at_unit_pos player_triarii circle 30;											;; Highlighter around player's ram
		point_at_card unit, slot 3, 3;
		while_not I_UnitSelected player_triarii;											;; Player left-click on highlighted unit
		end_while;
		stop_all_point_at_indicators;
		restart_timer Mediolanum_Pause_Timer;

		while I_TimerElapsed Mediolanum_Pause_Timer < 1000;									;Do nothing for a bit
    	end_while;
		;;camera_look_at_position GateCentre;
		inhibit_camera_input true;
		battle_wait 1;
		camera_zoom_to_bookmark 2;															;; Camera pans to the breach
		battle_wait 1;
		inhibit_camera_input false;
		point_at_location GateCentre;
		clear_restrict_battle_movement;
		restrict_battle_movement circle, label GateCentre, 10;
		filter_unit_commands on player_triarii;
		declare_counter TriariiMoving;
		monitor_event BattleUnitActionStatus BattleUnitActionStatus player_triarii moving;
			set_counter TriariiMoving 1;
			terminate_monitor;
		end_monitor;

		while I_CompareCounter TriariiMoving = 0;
		end_while;
		set_counter TriariiMoving 0;
		ui_flash_start advisor_continue;													;; Highlighter around Continue button
		while I_AdvisorVisible;																;; Player left-click on the highlighted button
		end_while;
		unit_order_turn barb_infantry_1 180 absolute;
		ui_flash_stop advisor_continue;
		restart_timer Mediolanum_Pause_Timer;

		while I_TimerElapsed Mediolanum_Pause_Timer < 1000;									;Do nothing for a bit
    	end_while;																			;once the time is up stop the timer
		restart_timer Mediolanum_Pause_Timer;
		disable_ui_card unit, 3;
		filter_unit_commands off player_triarii;
		block_unit_selection on player_triarii;
		while I_TimerElapsed Mediolanum_Pause_Timer < 1000;									;Do nothing for a bit
    	end_while;																			;once the time is up stop the timer
		remove_battle_map_arrow;
		while I_AdvisorSpeechPlaying;														;; Advisor panel is automatically closed
		end_while;																			;; New advise triggered
		battle_wait 2;



		;; Explain Siege Artillery

		advance_advice_thread Siege_Equipment_Siege_Artillery_1_type_b;							;; Message displayed in Marcus' panel
		restart_timer Mediolanum_Pause_Timer;
		while I_TimerElapsed Mediolanum_Pause_Timer < 2000;									;Do nothing for a bit
    	end_while;																			;once the time is up stop the timer
		;;camera_look_at_position BetweenArty; 												
		inhibit_camera_input true;
		battle_wait 1;
		camera_zoom_to_bookmark 9;															;; Camera pans to player's Onagers
		battle_wait 1;
		inhibit_camera_input false;
		block_unit_selection off player_onagers_1;
		restart_timer Mediolanum_Pause_Timer;
		while I_TimerElapsed Mediolanum_Pause_Timer < 2000;									;Do nothing for a bit
    	end_while;																			;once the time is up stop the timer
		point_at_unit_pos player_onagers_1 circle 30;
		;;remove_battle_map_arrow;
		enable_ui_card unit, 5;
		;;camera_zoom_to_unit player_onagers_1;
		;;inhibit_camera_input true;
		;;battle_wait 1;
		;;camera_zoom_to_bookmark 3;															;; Camera pans to the breach
		;;battle_wait 1;
		;;inhibit_camera_input false;
		point_at_card unit, slot 5, 6;
		while_not I_UnitSelected player_onagers_1;											;; Player left-click on highlighted unit
		end_while;
		filter_unit_commands on player_onagers_1;
		;;block_unit_selection on player_triarii;
		stop_all_point_at_indicators;
		clear_restrict_battle_movement;
		restrict_battle_movement circle, label Breach, 10;

		restart_timer Mediolanum_Pause_Timer;
		while I_TimerElapsed Mediolanum_Pause_Timer < 1000;									;Do nothing for a bit
    	end_while;																			;; Camera pans to enemy settlement walls (right in front of the unit)
		;;camera_look_at_position Breach;
		
		inhibit_camera_input true;
		battle_wait 1;
		camera_zoom_to_bookmark 10;															;; Camera pans to the breach
		battle_wait 1;
		inhibit_camera_input false;
		filter_unit_commands on player_onagers_1;
		point_at_location Breach;															;; Highlighter around location (close to the walls)
		declare_counter OnagersFiring;
		monitor_event BattleUnitActionStatus BattleUnitActionStatus player_onagers_1 missiles_firing;
			set_counter OnagersFiring 1;
			terminate_monitor;
		end_monitor;

		while I_CompareCounter OnagersFiring = 0;
		end_while;
		set_counter OnagersFiring 0;
		battle_wait 2;
		stop_all_point_at_indicators;
		remove_battle_map_arrow;
		;;while I_AdvisorSpeechPlaying;														;; Advisor panel is automatically closed
		;;end_while;	
		battle_wait 2;

		;;ui_flash_start advisor_continue;													;; Highlighter around Continue button
		;;while I_AdvisorVisible;																;; Player left-click on the highlighted button
		;;end_while;
		;;ui_flash_stop advisor_continue;
		disable_ui_card unit, 5;
		filter_unit_commands off player_onagers_1;
		block_unit_selection on player_onagers_1;
		advance_advice_thread Speed_Up_Battle_2_type_b;
		enable_ui play_button;
		ui_flash_start play_button;														;; Highlighter around Pause button
		declare_counter PlayButtonPressed;
		monitor_event ButtonPressed ButtonPressed play_button;

			set_counter PlayButtonPressed 1;

			terminate_monitor;
		end_monitor;

		inhibit_camera_input false;
		battle_wait 2;
		remove_battle_map_arrow;
		while I_CompareCounter PlayButtonPressed = 0;	
		end_while;
		set_counter PlayButtonPressed 0;													;; Game is un-paused
		ui_flash_stop play_button;
		disable_ui half_speed_button;
		;;enable_ui play_button;
		;;enable_ui ffwd_button;
		;;enable_ui ultra_ffwd_button;
		while_not I_BattleSettlementGateDestroyed TrueCondition;
		end_while;
		;;unit_order_move barb_infantry_2 30, -226;
		unit_order_move barb_horse 10, -200;

		while I_AdvisorSpeechPlaying;														;; Advisor panel is automatically closed
		end_while;																			;; New advise triggered
		unit_order_turn barb_horse 180 absolute;


		;; Use of Cavalry
		while_not I_BattleSettlementWallsBreached TrueCondition;
		end_while;
		filter_unit_commands off player_triarii;
		battle_wait 5;
		unit_order_attack_unit player_triarii barb_chieftain run;
		unit_order_attack_unit player_hastati_1 barb_chieftain run;
		unit_order_attack_unit player_hastati_2 barb_infantry_1 run;
		unit_order_attack_unit barb_chieftain player_triarii run;
		unit_order_attack_unit barb_horse player_triarii run;
		unit_order_attack_unit barb_infantry_1 player_hastati_1 run;
		if I_UnitDestroyed barb_chieftain
			unit_order_attack_unit player_triarii barb_infantry_1 run;
		end_if;
		battle_wait 2;
		advance_advice_thread Use_Your_Cavalry_1_type_b;											;; Message displayed in Marcus' panel
		enable_ui_card unit, 0;
		enable_ui_card unit, 5;
		block_unit_selection off player_mercCav;
		block_unit_selection off player_general;
		filter_unit_commands off player_mercCav;
		filter_unit_commands off player_general;
		battle_wait 2;
		inhibit_camera_input true;
		battle_wait 1;
		camera_zoom_to_bookmark 7;															;; Camera pans to player's cavalry
		battle_wait 1;
		inhibit_camera_input false;													
		;;point_at_unit_pos player_mercCav circle 30;											;; Highlighter around player's cavalry
		point_at_card unit, slot 0, 0;
		point_at_card unit, slot 1, 5;

		while_not I_SpecificUnitsSelected player_general player_mercCav;											;; Player left-click on highlighted unit
		end_while;
		ui_card_selection_lock on;
		stop_all_point_at_indicators;
		inhibit_camera_input true;
		battle_wait 1;
		camera_zoom_to_bookmark 4;															;; Camera pans to the breach
		battle_wait 1;
		inhibit_camera_input false;
		point_at_location CavBreach;
		filter_unit_commands on player_mercCav;
		filter_unit_commands on player_general;															;; Highlighter around location
		clear_restrict_battle_movement;
		restrict_battle_movement circle, label CavBreach, 8;
		while_not I_UnitDistanceFromPosition player_mercCav CavBreach < 40;					;; Player right-click on the breach
			point_at_location CavBreach;														;; Cavalry moves to selected location
		end_while;
		while_not I_UnitDistanceFromPosition player_general CavBreach < 40;					;; Player right-click on the breach
			point_at_location CavBreach;														;; Cavalry moves to selected location
		end_while;
		ui_card_selection_lock off;
		battle_wait 3;
		remove_battle_map_arrow;
		clear_restrict_battle_movement;
		restrict_battle_movement circle, label InfBreach, 140;



		;; Camera pans to enemy unit by the gate walls (right in front of the unit)
		;;camera_zoom_to_unit barb_horse;
		inhibit_camera_input true;
		battle_wait 1;
		camera_zoom_to_bookmark 11;															;; Camera pans to the gate
		battle_wait 1;
		inhibit_camera_input false;
		point_at_unit_pos barb_horse circle 30;										;; Highlighter around enemy unit (close to the walls)
		;;while_not I_IsUnitRouting barb_horse;
		;;end_while;																			;; Player right-click (or double right-click) on the highlighted unit
		while_not I_IsUnitEngaged player_mercCav;												;; Player double right-click on the highlighted units
		end_while;
		while_not I_IsUnitEngaged player_general;												;; Player double right-click on the highlighted units
		end_while;
		remove_battle_map_arrow;
		while I_CompareCounter GeneralDead = 0;										;; Waiting for the enemy general to die
		end_while;
		battle_wait 2;

		;; Take the square
		block_unit_selection off player_archers;
		block_unit_selection off player_triarii;
		block_unit_selection off player_hastati_1;
		block_unit_selection off player_hastati_2;
		block_unit_selection off player_onagers_1;
		filter_unit_commands on player_archers;
		filter_unit_commands on player_triarii;
		filter_unit_commands on player_hastati_1;
		filter_unit_commands on player_hastati_2;
		filter_unit_commands on player_onagers_1;
		box_drag_selection on;
		enable_ui_card unit, 1;
		enable_ui_card unit, 2;
		enable_ui_card unit, 3;
		enable_ui_card unit, 4;
		enable_ui_card unit, 5;
		advance_advice_thread Take_The_Square_1_type_b;											;; Message displayed in Marcus' panel
		battle_wait 5;
		inhibit_camera_input true;
		battle_wait 1;
		camera_zoom_to_bookmark 5;															;; Camera pans to the breach
		battle_wait 1;
		inhibit_camera_input false;														;; Camera pans to settlement square
		;;battle_wait 4;
		;;camera_zoom_to_bookmark 6;														;; Camera pans back to player's army
		enable_ui feral_select_all_units;
		ui_flash_start feral_select_all_units;												;; Highlighter around Select All button
		declare_counter SelectButtonPressed;
		monitor_event ButtonPressed ButtonPressed feral_select_all_units;

		set_counter SelectButtonPressed 1;

			terminate_monitor;
		end_monitor;


		while I_CompareCounter SelectButtonPressed = 0;										;; Player left-click on the highlighted button
		end_while;
		set_counter SelectButtonPressed 0;													;; All units selected
		ui_flash_stop feral_select_all_units;
		battle_wait 1;
		;;ui_flash_start feral_select_all_units;												;; Highlighter around Select All button
		;;declare_counter SelectButtonPressed;
		;;monitor_event ButtonPressed ButtonPressed feral_select_all_units;

		;;set_counter SelectButtonPressed 1;

		;;	terminate_monitor;
		;;end_monitor;


		;;while I_CompareCounter SelectButtonPressed = 0;										;; Player left-click on the highlighted button
		;;end_while;
		;;set_counter SelectButtonPressed 0;													;; All units selected
		;;ui_flash_stop feral_select_all_units;
		;;battle_wait 2;

		;;camera_zoom_to_bookmark 5;														;; Camera pans to settlement square
		point_at_location TownCentre;														;; Highlighter around location (square)
		clear_restrict_battle_movement;
		unit_unset_morale barb_infantry_2;
		;;restrict_battle_movement circle, label TownCentre, 70;
		if I_IsUnitEngaged barb_infantry_2;
			remove_battle_map_arrow;
			;;clear_restrict_battle_movement;
			;;unit_unset_morale barb_infantry_2;
		end_if;




		;; Congratulations!

		declare_counter ConditionsMet;
		monitor_event ButtonPressed ButtonPressed conditions_complete_quit_button;

			set_counter ConditionsMet 1;

			terminate_monitor;
		end_monitor;

		monitor_event ScrollDidOpen ScrollDidOpen conditions_complete_scroll;
			enable_ui conditions_complete_scroll;
			enable_ui conditions_complete_quit_button;
			disable_ui conditions_complete_continue_button;
			remove_battle_map_arrow;
			ui_flash_start conditions_complete_quit_button;
			terminate_monitor;
		end_monitor;

		while I_CompareCounter ConditionsMet = 0;
		end_while;

		declare_counter MediolaniumResultButtonPressed;
		monitor_event ButtonPressed ButtonPressed post_battle_stats_show_results_button;

			set_counter MediolaniumResultButtonPressed 1;

			terminate_monitor;
		end_monitor;

		while I_CompareCounter MediolaniumResultButtonPressed = 0;
		end_while;
		enable_entire_ui;
		ui_flash_stop post_battle_stats_show_results_button;
		ui_flash_stop conditions_complete_quit_button;
