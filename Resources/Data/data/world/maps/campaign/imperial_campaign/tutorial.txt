;;;;*************************************************************************************************
;;;;	Tutorial progress
;;;;*************************************************************************************************

if I_IsTutorialEnabled;
	declare_prologue;

	suspend_during_battle on;


;;;;*************************************************************************************************
;;;;	Checking to see if the player has captured settlements
;;;;*************************************************************************************************

	declare_counter SegestaCaptured;
	monitor_event GeneralCaptureSettlement FactionType romans_julii
		and SettlementName Segesta

		set_counter SegestaCaptured 1;

		terminate_monitor;
	end_monitor;


;;;;*************************************************************************************************
;;;;	Additional Event Monitors
;;;;*************************************************************************************************

	monitor_event ScrollDidOpen ScrollDidOpen more_info_scroll;										;;Necessary to fix fbug://RHD-3074
		enable_ui close_animated_frame_button;
		terminate_monitor;
	end_monitor;

	monitor_event ScrollOpened ScrollOpened char_info_scroll;										;;Necessary to fix fbug://RHD-1726
		enable_ui feral_character_details_button;
		terminate_monitor;
	end_monitor;

;;;;*************************************************************************************************
;;;;	Declare all needed counters at the top of the script to keep track of them better
;;;;*************************************************************************************************

		declare_counter Completed_Camera_Moves;
		declare_counter Completed_Camera_Moves_Zoom;
		declare_counter Completed_Camera_Moves_Rot;
		declare_counter DeselectActionComplete;
		declare_counter AssaultButtonPressed;
		declare_counter FightButtonPressed;
		declare_counter BattleFightPressed;
		declare_counter BattleResolvePressed;
		declare_counter ResolveStatsDissmissed;
		declare_counter EnslaveButtonPressed;
		declare_counter SegestaSelected;
		declare_counter governors_house_queued;
		declare_counter GovHouseSelected;
		declare_counter AriminumSelected;
		declare_counter PortSelected;
		declare_counter ArretiumSelected;
		declare_counter RangeSelected;
		declare_counter MilitiaSelected;
		declare_counter SendAgentPressed;
		declare_counter ListPopulated;
		declare_counter DeciusPressed;
        declare_counter MediolaniumSelected;
		declare_counter PressEndTurn;
		declare_counter SenateButtonPressed;
		declare_counter CloseDialogPressed;
		declare_counter MapDemandPressed;
		declare_counter DiplomacyStarted;
		declare_counter TradeOfferPressed;
		declare_counter MapOfferPressed;
		declare_counter MakeOfferPressed;
		declare_counter UnitsTabPressed;
		declare_counter BuildingsTabPressed;
		declare_counter SenateZoomPressed;
		declare_counter AdviceAudioPlaying;

;;;;*************************************************************************************************
;;;; ACTUAL TUTORIAL NOW STARTS
;;;;*************************************************************************************************

		disable_entire_ui;
		disable_all_ui_cards unit;
		disable_all_ui_cards building;
		disable_shortcuts true;
		inhibit_camera_input true;


		enable_ui campaign_map_pause_button;
		enable_ui type_c_advice_left_arrow;
        enable_ui type_c_advice_right_arrow;
        enable_ui type_e_advice_left_arrow;
        enable_ui type_e_advice_right_arrow;
		enable_ui type_c_advisor_highlight;
		enable_ui type_c_advisor_button;
		enable_ui tutorial_hd_frame_a_replay;
		enable_ui tutorial_hd_frame_b_replay;
		enable_ui tutorial_hd_frame_d_replay;
		enable_ui tutorial_hd_frame_e_replay;
		enable_ui wonder_close_button;
		enable_ui tooltip;
		enable_ui pause_menu;

		disable_ui show_senate_mission_tab;
		disable_ui show_news_tab;
		disable_ui show_alerts_tab;
		disable_ui show_reports_tab;


		toggle_minimap;
		disable_ui advisor_portrait_button;
		suspend_unscripted_advice true;
		campaign_wait 0.1;
		set_strat_camera_speed 6;
		reposition_character Vibius Julius, 96, 82;
		reposition_character Decius Curtius, 90, 85;
		reposition_character Sextus Antio, 108, 54;
		reposition_character Oppius Clausus, 107, 54;
		reposition_character Timasion, 91, 47;
		spawn_army;
			faction romans_julii;
				character Luca, general, age 29, , x 91, y 80;
				unit		roman city militia			soldiers 0 exp 1 armour 1 weapon_lvl 1;
				unit		roman city militia			soldiers 0 exp 1 armour 1 weapon_lvl 1;
		end;
		spawn_army;
			faction slave, sub_faction Gauls;
				character Casticos, general, age 29, , x 83, y 84;
				unit		barb archer slave			soldiers 0 exp 0 armour 0 weapon_lvl 0;
		end;
		clear_restrict_strat_movement;
		restrict_strat_movement romans_julii, 89, 82;
		strat_selection_unblocker settlement on none;
		strat_selection_unblocker character on none;
		deselect_current_selection;
		
		campaign_wait 3.5;
		zoom_strat_camera 0.4;
		force_agent_succeed yes;
		force_diplomacy accept, yes;
		ai_active_set off;
		set_marriage_allowed off;
		
		deselect_current_selection;
		
		

		;;Introduction to Victoria (Advisor)
		advance_advice_thread Introduction_Victoria;
		enable_ui advisor_continue;
		while I_AdvisorSpeechPlaying;
		end_while;
		ui_flash_start advisor_continue circle;
		while I_AdvisorVisible;	
		end_while;
		ui_flash_stop advisor_continue;
		campaign_wait 1;
		zoom_strat_camera 0.7;



		;;Introduction to Rome and the Sons of Mars
    	campaign_wait 1;
    	move_to_settlement Rome;
    	zoom_strat_camera 0.3;
    	campaign_wait 1;
    	advance_advice_thread Sons_of_Mars_and_Rome_type_e;
    	settlement_flash_start Rome;

    	declare_counter destroy_monitor;
    	monitor_event AdvisorAudioStopped AdvisorAudioStopped;
    		if I_CompareCounter destroy_monitor = 1;
    			terminate_monitor;
    		end_if;
    		set_counter AdviceAudioPlaying 1;
    	end_monitor;
    	
    	while I_CompareCounter AdviceAudioPlaying = 0;
    		if_not I_AdvisorSpeechPlaying;
    			set_counter AdviceAudioPlaying 1;
    		end_if;
    	end_while;
    	set_counter AdviceAudioPlaying 0;
    	settlement_flash_stop Rome;
    	stop_all_point_at_indicators;
    	move_strat_camera 93, 81;
    	campaign_wait 0.5;
    	set_advice_page 2;	
    	
    	while I_CompareCounter AdviceAudioPlaying = 0;
    		campaign_wait 5;
    		settlement_flash_start Arretium;
    		campaign_wait 1.8;
    		settlement_flash_stop Arretium;
    		settlement_flash_start Ariminum;
    		campaign_wait 1.8;
    		settlement_flash_stop Ariminum;
    		character_flash_start Flavius Julius;
    		campaign_wait 1.8;
    		character_flash_stop Flavius Julius;
    		if_not I_AdvisorSpeechPlaying;
    			set_counter AdviceAudioPlaying 1;
    		end_if;
    	end_while;
    	set_counter AdviceAudioPlaying 0;
    	
    	zoom_strat_camera 0.9;
    	enable_ui advisor_continue_type_e;
    	while I_AdvisorSpeechPlaying;
		end_while;
    	campaign_wait 0.5;
    	set_advice_page 3;
    	while I_AdvisorSpeechPlaying;
		end_while;
    	ui_flash_start advisor_continue_type_e circle;
		while I_AdvisorVisible;
		end_while;
		disable_ui advisor_continue_type_e;
		set_counter destroy_monitor 1;
		ui_flash_stop advisor_continue_type_e;



		;;Introduction to Campaign Map & Goals
		disable_specific_shortcut camera step_l false;
		disable_specific_shortcut camera rot_l false;
		disable_specific_shortcut camera step_r false;
		disable_specific_shortcut camera rot_r false;
		disable_specific_shortcut camera step_fwd false;
		disable_specific_shortcut camera step_bck false;
		disable_specific_shortcut camera mousescr_out false;
		disable_specific_shortcut camera mousescr_up false;
		disable_specific_shortcut strat_ui zoom_in false;
		disable_specific_shortcut strat_ui zoom_out false;
    	advance_advice_thread Campaign_Map_Basics_1_type_b;
    	inhibit_camera_input false;

    	declare_counter StepL;
		monitor_event ShortcutTriggered ShortcutTriggered camera::step_l;
			inc_counter Completed_Camera_Moves 1;
			inc_counter StepL 1;
			if I_CompareCounter Completed_Camera_Moves = 4;
				terminate_monitor;
			end_if;
		end_monitor;

		declare_counter StepR;
		monitor_event ShortcutTriggered ShortcutTriggered camera::step_r;
			inc_counter Completed_Camera_Moves 1;
			inc_counter StepR 1;
			if I_CompareCounter Completed_Camera_Moves = 4;
				terminate_monitor;
			end_if;
		end_monitor;

		declare_counter StepFWD;
		monitor_event ShortcutTriggered ShortcutTriggered camera::step_fwd;
			inc_counter Completed_Camera_Moves 1;
			inc_counter StepFWD 1;
			if I_CompareCounter Completed_Camera_Moves = 4;
				terminate_monitor;
			end_if;
		end_monitor;

		declare_counter StepBCK;
		monitor_event ShortcutTriggered ShortcutTriggered camera::step_bck;
			inc_counter Completed_Camera_Moves 1;
			inc_counter StepBCK 1;
			if I_CompareCounter Completed_Camera_Moves = 4;
				terminate_monitor;
			end_if;
		end_monitor;

		while I_CompareCounter StepL = 0;
		end_while;
		while I_CompareCounter StepR = 0;
		end_while;
		while I_CompareCounter StepFWD = 0;
		end_while;
		while I_CompareCounter StepBCK = 0;
		end_while;
		campaign_wait 0.2;
		while I_CompareCounter Completed_Camera_Moves < 4;
		end_while;
		advance_completed_tasks 2;

		declare_counter MouseOUT;
		monitor_event ShortcutTriggered ShortcutTriggered camera::zoom_out;
			inc_counter Completed_Camera_Moves_Zoom 1;
			inc_counter MouseOUT 1;
			if I_CompareCounter Completed_Camera_Moves_Zoom = 2;
				terminate_monitor;
			end_if;
		end_monitor;

		declare_counter MouseUP;
		monitor_event ShortcutTriggered ShortcutTriggered camera::zoom_in;
			inc_counter Completed_Camera_Moves_Zoom 1;
			inc_counter MouseUP 1;
			if I_CompareCounter Completed_Camera_Moves_Zoom = 2;
				terminate_monitor;
			end_if;
		end_monitor;

		while I_CompareCounter MouseOUT = 0;
		end_while;
		while I_CompareCounter MouseUP = 0;
		end_while;
		campaign_wait 0.2;
		while I_CompareCounter Completed_Camera_Moves_Zoom < 2;
		end_while;
		advance_completed_tasks 3;

		declare_counter RotL;
		monitor_event ShortcutTriggered ShortcutTriggered camera::rot_l;
			inc_counter Completed_Camera_Moves_Rot 1;
			inc_counter RotL 1;
			if I_CompareCounter Completed_Camera_Moves_Rot = 2;
				terminate_monitor;
			end_if;
		end_monitor;

		declare_counter RotR;
		monitor_event ShortcutTriggered ShortcutTriggered camera::rot_r;
			inc_counter Completed_Camera_Moves_Rot 1;
			inc_counter RotR 1;
			if I_CompareCounter Completed_Camera_Moves_Rot = 2;
				terminate_monitor;
			end_if;
		end_monitor;

		while I_CompareCounter RotL = 0;
		end_while;
		while I_CompareCounter RotR = 0;
		end_while;
		campaign_wait 0.2;
		while I_CompareCounter Completed_Camera_Moves_Rot < 2;
		end_while;
		advance_completed_tasks 4;
    	enable_ui advisor_continue_type_d;



		;; Mission assigned: Select an army
		senate_mission_take_city one_triarii, Segesta;
		advance_advice_thread New_Mission_Assigned_1_type_b;
		campaign_wait 3;
    	declare_counter monitor_term_check;
    	declare_counter monitor_term_check_tab;
    	declare_counter pause_opened;
		set_label senate_tab_section;
		disable_ui show_senate_mission_tab;
		disable_ui close_trigger;
		disable_ui message_zoom_to_button;
		set_counter	SenateButtonPressed 0;
		set_counter	SenateZoomPressed 0;
		set_counter pause_opened 0;

		;; Open senate mission tab
    	monitor_event ButtonPressed ButtonPressed show_senate_mission_tab;
			if I_CompareCounter monitor_term_check_tab = 1;
				terminate_monitor;
			end_if;
			set_counter	SenateButtonPressed 1;
		end_monitor;
		ui_flash_start show_senate_mission_tab circle;
		enable_ui show_senate_mission_tab;
		while I_CompareCounter SenateButtonPressed = 0;
		end_while;
		disable_ui show_senate_mission_tab;
		ui_flash_stop show_senate_mission_tab;
		advance_completed_tasks 2;

		monitor_event ScrollOpened ScrollOpened pause_menu;
			if I_CompareCounter monitor_term_check = 1;
				terminate_monitor;
			end_if;
			set_counter pause_opened 1;
		end_monitor;

		;; Press zoom to mission button
		monitor_event ButtonPressed ButtonPressed message_zoom_to_button;
			set_counter	SenateZoomPressed 1;
			if I_CompareCounter monitor_term_check = 1;
				terminate_monitor;
			end_if;
		end_monitor;
		campaign_wait 3;

		ui_flash_start message_zoom_to_button circle;
    	enable_ui message_zoom_to_button;
		while I_CompareCounter SenateZoomPressed = 0;
			if I_CompareCounter pause_opened = 1;
				set_counter pause_opened 0;
				set_counter SenateZoomPressed 1;
				goto senate_tab_section;
			end_if;
		end_while;
		disable_ui message_zoom_to_button;
		ui_flash_stop message_zoom_to_button;

		set_counter	SenateZoomPressed 0;
		advance_completed_tasks 3;
		campaign_wait 1;
		settlement_flash_start Segesta;

		;; Close senate mission tab
		set_counter SenateButtonPressed 0;
		ui_flash_start show_senate_mission_tab circle;
		enable_ui show_senate_mission_tab;
		monitor_event ButtonPressed ButtonPressed show_senate_mission_tab;
			set_counter	SenateButtonPressed 1;
			terminate_monitor;
		end_monitor;
		while I_CompareCounter SenateButtonPressed = 0;
			if I_CompareCounter pause_opened = 1;
				set_counter SenateButtonPressed 1;
			end_if;
		end_while;
		disable_ui show_senate_mission_tab;
		ui_flash_stop show_senate_mission_tab;
		advance_completed_tasks 4;
		enable_ui close_trigger;
		set_counter monitor_term_check 1;
		set_counter monitor_term_check_tab 1;
		stop_all_point_at_indicators;
		


		;;Attack Segesta and force player to fight
		monitor_event ItemDeselected TrueCondition;
			if I_CompareCounter DeselectActionComplete = 1;
				set_counter DeselectActionComplete 0;
				terminate_monitor;
			end_if;
			stop_all_point_at_indicators;
			goto labelOne;
		end_monitor;

		advance_advice_thread How_To_Attack_A_Settlement_1_type_b;
		set_label labelOne;
		strat_selection_unblocker character on Flavius Julius;
		point_at_character, slot 0, Flavius Julius, circle;	
		while_not I_CharacterSelected Flavius Julius;
		end_while;
		stop_all_point_at_indicators;
		advance_completed_tasks 2;
		point_at_strat_position, slot 1, 83, 84, y_offset -70;																		
		clear_restrict_strat_movement;
		restrict_strat_movement romans_julii, 89, 82;
		restrict_strat_movement romans_julii, 83, 84;
		while_not I_CharacterTypeNearTile romans_julii named_character, 1.45, 83 ,84;
		end_while;
		advance_completed_tasks 3;
		set_counter DeselectActionComplete 1;
		force_deselect_trigger;
		stop_all_point_at_indicators;
		settlement_flash_stop Segesta;
		set_strat_camera_speed 15;



		;;Siege overview screen
		monitor_event ScrollOpened ScrollOpened seige_scroll;
			enable_ui strat_ui;
			enable_ui campaign_hud;
			enable_ui siege_assault_button;
			disable_ui siege_maintain_button;
			disable_ui siege_end_button;
			disable_ui siege_equipment_icon_ram;
			disable_ui siege_equipment_icon_ladder;
			disable_ui siege_equipment_icon_siege_tower;
			disable_ui siege_equipment_icon_sap_points;
			advance_advice_thread Siege_Overview_1_type_b;	
			ui_flash_start siege_assault_button circle;	
			terminate_monitor;
		end_monitor;


		monitor_event ButtonPressed ButtonPressed siege_assault_button;
			set_counter AssaultButtonPressed 1;
			terminate_monitor;
		end_monitor;
		while I_CompareCounter AssaultButtonPressed = 0;
		end_while;
		set_counter AssaultButtonPressed 0;



		;;Battle details  (See Segesta battle tutorial)
		monitor_event ScrollOpened ScrollOpened prebattle_scroll;
			enable_ui prebattle_fight_button;
			enable_ui prebattle_auto_resolve_button;
			disable_ui prebattle_withdraw_button;
			ui_flash_stop siege_assault_button;
			ui_flash_start prebattle_fight_button circle;
			advance_advice_thread Battle_Details_1_type_b;	
			terminate_monitor;
		end_monitor;

		monitor_event ButtonPressed ButtonPressed prebattle_fight_button;
			set_counter FightButtonPressed 1;
			set_counter BattleFightPressed 1;
			terminate_monitor;
		end_monitor;

		monitor_event ButtonPressed ButtonPressed prebattle_auto_resolve_button;
			set_counter FightButtonPressed 1;
			set_counter BattleResolvePressed 1;
			advance_advice_thread Battle_Results_Screen_1_type_b;
			terminate_monitor;
		end_monitor;

		while I_CompareCounter FightButtonPressed = 0;
		end_while;
		set_counter FightButtonPressed 0;

		ui_flash_stop prebattle_fight_button;
		disable_ui stop_tutorial_button;


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;; SEGESTA BATTLE
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
		
		if I_CompareCounter BattleFightPressed = 1;
			set_counter ResolveStatsDissmissed 1;
			include_script battle_segesta.txt;
		end_if;



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;; POST-BATTLE TURN 1
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

		;; Victory
		while I_InBattle TrueCondition;
		end_while;
		force_settlement_tab buildings;
		
		disable_entire_ui;
		enable_ui post_battle_scroll;
		enable_ui post_battle_stats_show_results_button;
		disable_all_ui_cards unit;
		disable_all_ui_cards building;
		disable_ui agent_confirm_button;
		enable_ui campaign_map_pause_button;
		enable_ui advisor_continue;
		enable_ui type_c_advisor_highlight;
		enable_ui type_c_advisor_button;
		enable_ui type_c_advice_left_arrow;
        enable_ui type_c_advice_right_arrow;
        enable_ui type_e_advice_left_arrow;
        enable_ui type_e_advice_right_arrow;
		enable_ui wonder_close_button;
		enable_ui tooltip;
		enable_ui stop_tutorial_button;
		enable_ui tutorial_hd_frame_a_replay;
		enable_ui tutorial_hd_frame_b_replay;
		enable_ui tutorial_hd_frame_d_replay;
		enable_ui tutorial_hd_frame_e_replay;

		disable_ui show_senate_mission_tab;
		disable_ui show_news_tab;
		disable_ui show_alerts_tab;
		disable_ui show_reports_tab;

		suspend_unscripted_advice true;

		monitor_event ButtonPressed ButtonPressed post_battle_stats_show_results_button;
			set_counter ResolveStatsDissmissed 1;
			terminate_monitor;
		end_monitor;

		if I_CompareCounter BattleResolvePressed = 1;
			ui_flash_start post_battle_stats_show_results_button circle;
		end_if;

		while I_CompareCounter ResolveStatsDissmissed = 0;
		end_while;

		disable_shortcuts true;
		disable_specific_shortcut camera step_l false;
		disable_specific_shortcut camera rot_l false;
		disable_specific_shortcut camera step_r false;
		disable_specific_shortcut camera rot_r false;
		disable_specific_shortcut camera step_fwd false;
		disable_specific_shortcut camera step_bck false;
		disable_specific_shortcut camera mousescr_out false;
		disable_specific_shortcut camera mousescr_up false;
		disable_specific_shortcut strat_ui zoom_in false;
		disable_specific_shortcut strat_ui zoom_out false;
		clear_restrict_strat_movement;
		restrict_strat_movement romans_julii, 83, 84;
		inhibit_camera_input true;
		while_not GeneralCaptureSettlement TrueCondition;
		end_while;
		
		

		;; Explain Occupy, enslave and exterminate
		disable_ui campaign_map_pause_button;
		disable_pause_shortcut_in_campaign true;
		campaign_wait 2;
		disable_ui loot_settlement_occupy_button;
		disable_ui loot_settlement_enslave_button;
		disable_ui loot_settlement_extermintate_button;
		advance_advice_thread What_Now_1_type_e;

		monitor_event ButtonPressed ButtonPressed loot_settlement_enslave_button;
			set_counter EnslaveButtonPressed 1;
			terminate_monitor;
		end_monitor;
		
		while I_AdvisorSpeechPlaying;
		end_while;
		campaign_wait 0.5;	
		set_advice_page 2;
		while I_AdvisorSpeechPlaying;
		end_while;
		campaign_wait 0.5;
		set_advice_page 3;
		while I_AdvisorSpeechPlaying;
		end_while;
		campaign_wait 0.5;
		set_advice_page 4;
		while I_AdvisorSpeechPlaying;
		end_while;
		campaign_wait 0.5;
		set_advice_page 5;
		ui_flash_start loot_settlement_enslave_button square;
		while I_CompareCounter EnslaveButtonPressed = 0;
			disable_ui loot_settlement_occupy_button;n;
			disable_ui loot_settlement_extermintate_button;
			enable_ui loot_settlement_enslave_button;																		
		end_while;
		ui_flash_stop loot_settlement_enslave_button;
		enable_ui campaign_map_pause_button;
		disable_pause_shortcut_in_campaign false;
		deselect_current_selection;
		set_counter EnslaveButtonPressed 0;
		inhibit_camera_input false;
		campaign_wait 2;
		enable_ui news_list_items;
		enable_ui show_alerts_tab;
		advance_advice_thread Congratulations_1;
		while I_AdvisorSpeechPlaying;
		end_while;
		campaign_wait 0.5;
		ui_flash_start advisor_continue circle;
		while I_AdvisorVisible;
		end_while;
		ui_flash_stop advisor_continue;
		strat_selection_unblocker character off Flavius Julius;
		strat_selection_unblocker settlement on Segesta;
		monitor_event ItemDeselected TrueCondition;
			if I_CompareCounter DeselectActionComplete = 1;
				set_counter DeselectActionComplete 0;
				terminate_monitor;
			end_if;
			disable_ui open_construction_options;
			goto labelTwo;
		end_monitor;
		advance_advice_thread What_Now_2_type_b;
		set_label labelTwo;
		point_at_settlement Segesta, circle;
		monitor_event SettlementSelected SettlementName Segesta;
			set_counter SegestaSelected 1;
			terminate_monitor;
		end_monitor;
		while I_CompareCounter SegestaSelected = 0;
		end_while;
		force_settlement_tab buildings; 
		stop_all_point_at_indicators;



		;; Settlement overview with help overlay
		advance_advice_thread Controlling_Your_Empire_1;
		disable_ui view_settlement;
		disable_ui make_capital;
		while I_AdvisorSpeechPlaying;
		end_while;
		ui_flash_start advisor_continue circle;
		while I_AdvisorVisible;
		end_while;
		ui_flash_stop advisor_continue;
		campaign_wait 0.2;



		;; Tutorial Resumes: Build Governor's House in Segesta
		advance_advice_thread Construction_Basics_1_type_b;
		campaign_wait 2;
		enable_ui open_construction_options;
		ui_flash_start open_construction_options circle;
		monitor_event ConstructionPopulated TrueCondition;
			if I_CompareCounter governors_house_queued = 1;
				terminate_monitor;
			end_if;
			ui_flash_stop open_construction_options;
			disable_all_ui_cards construction;
			point_at_card construction governors_house, square;
			enable_ui_card construction governors_house;
			advance_completed_tasks 2;
			advance_advice_thread Lets_Build_1_type_b;
			advance_completed_tasks 2;
			if I_CompareCounter DeselectActionComplete = 1;
				terminate_monitor;
			end_if;
		end_monitor;
		monitor_event ScrollOpened ScrollOpened building_info_scroll;
			advance_completed_tasks 3;
			if I_CompareCounter DeselectActionComplete = 1;
                terminate_monitor;
            end_if;
		end_monitor;
		campaign_wait 0.2;
		monitor_event ConstructionItemClicked ConstructionItemClicked governors_house;
			stop_all_point_at_indicators;
			disable_all_ui_cards construction;
			set_counter GovHouseSelected 1;
			set_counter governors_house_queued 1;
			if I_CompareCounter DeselectActionComplete = 1;
				terminate_monitor;
			end_if;
		end_monitor;
		while I_CompareCounter GovHouseSelected = 0;
		end_while;
		advance_completed_tasks 4;
		disable_all_ui_cards construction;
		disable_all_ui_cards construction;
		disable_ui open_construction_options;
		ui_flash_stop open_construction_options;
		set_counter DeselectActionComplete 1;
		force_deselect_trigger;
		campaign_wait 2;
		deselect_current_selection;
		campaign_wait 2;



		advance_advice_thread Economic_Buildings_1;
		while I_AdvisorSpeechPlaying;
		end_while;
		ui_flash_start advisor_continue circle;
		while I_AdvisorVisible;
		end_while;
		ui_flash_stop advisor_continue;



		;; Build Port in Ariminum (economic structure)
		monitor_event ItemDeselected TrueCondition;
			if I_CompareCounter DeselectActionComplete = 1;
				set_counter DeselectActionComplete 0;
				terminate_monitor;
			end_if;
			disable_ui open_construction_options;
			goto labelThree;
		end_monitor;
		deselect_current_selection;
		strat_selection_unblocker settlement off Segesta;
		strat_selection_unblocker settlement on Ariminum;
		advance_advice_thread Economic_Buildings_1_type_b;
		set_label labelThree;
		point_at_settlement Ariminum, circle;
		campaign_wait 0.1;
		monitor_event SettlementSelected SettlementName Ariminum;
			if I_CompareCounter DeselectActionComplete = 1;
				terminate_monitor;
			end_if;
			set_counter AriminumSelected 1;
		end_monitor;
		while I_CompareCounter AriminumSelected = 0;
		end_while;
		set_counter AriminumSelected 0;
		stop_all_point_at_indicators;
		campaign_wait 1;
		enable_ui open_construction_options;
		ui_flash_start open_construction_options circle;
		monitor_event ConstructionPopulated TrueCondition;
			if I_CompareCounter DeselectActionComplete = 1;
				terminate_monitor;
			end_if;
			ui_flash_stop open_construction_options;
			point_at_card construction port, square;
			disable_all_ui_cards construction;
			enable_ui_card construction port;
			advance_completed_tasks 2;
			disable_ui open_recruitement_options;
		end_monitor;
		monitor_event ScrollOpened ScrollOpened building_info_scroll;
			if I_CompareCounter DeselectActionComplete = 1;
                terminate_monitor;
            end_if;
            advance_completed_tasks 3;
		end_monitor;
		monitor_event ConstructionItemClicked ConstructionItemClicked port;
			set_counter PortSelected 1;
			stop_all_point_at_indicators;
			disable_all_ui_cards construction;
			terminate_monitor;
		end_monitor;
		while I_CompareCounter PortSelected = 0;
		end_while;
		advance_completed_tasks 4;
		disable_all_ui_cards construction;
		disable_ui open_construction_options;
		ui_flash_stop open_construction_options;
		set_counter DeselectActionComplete 1;
		force_deselect_trigger;
		campaign_wait 1;
		deselect_current_selection;
		strat_selection_unblocker settlement off Ariminum;
		campaign_wait 1;



		;; Build Practice Range in Arretium (military structure)
		monitor_event ItemDeselected TrueCondition;
			if I_CompareCounter DeselectActionComplete = 1;
				set_counter DeselectActionComplete 0;
				terminate_monitor;
			end_if;
			disable_ui open_construction_options;
			goto labelFour;
		end_monitor;
		advance_advice_thread Military_Buildings_1_type_b;
		strat_selection_unblocker settlement on Arretium;
		set_label labelFour;
		point_at_settlement Arretium, circle;
		strat_selection_unblocker settlement off Ariminum;
		strat_selection_unblocker settlement on Arretium;
		monitor_event SettlementSelected SettlementName Arretium;
			if I_CompareCounter DeselectActionComplete = 1;
				terminate_monitor;
			end_if;
			set_counter ArretiumSelected 1;
		end_monitor;
		while I_CompareCounter ArretiumSelected = 0;
		end_while;
		set_counter ArretiumSelected 0;
		campaign_wait 0.2;
		stop_all_point_at_indicators;
		campaign_wait 1;
		enable_ui open_construction_options;
		ui_flash_start open_construction_options circle;
		monitor_event ConstructionPopulated TrueCondition;
			if I_CompareCounter DeselectActionComplete = 1;
				terminate_monitor;
			end_if;
			ui_flash_stop open_construction_options;
			point_at_card construction practice_field, square;
			disable_all_ui_cards construction;
			advance_completed_tasks 2;
			enable_ui_card construction practice_field;
		end_monitor;
		monitor_event ScrollOpened ScrollOpened building_info_scroll;
			if I_CompareCounter DeselectActionComplete = 1;
                terminate_monitor;
            end_if;
            advance_completed_tasks 3;
		end_monitor;
		monitor_event ConstructionItemClicked ConstructionItemClicked practice_field;
			stop_all_point_at_indicators;
			set_counter RangeSelected 1;
			disable_all_ui_cards construction;
			if I_CompareCounter DeselectActionComplete = 1;
                terminate_monitor;
            end_if;
		end_monitor;
		while I_CompareCounter RangeSelected = 0;
		end_while;
		advance_completed_tasks 4;
		disable_all_ui_cards construction;
		disable_ui open_construction_options;
		ui_flash_stop open_construction_options;
		set_counter DeselectActionComplete 1;
		force_deselect_trigger;	
		campaign_wait 1;
		deselect_current_selection;
		campaign_wait 1;
		


		advance_advice_thread Settlements_And_Garrisons_1;
		while I_AdvisorSpeechPlaying;
		end_while;
		ui_flash_start advisor_continue circle;
		while I_AdvisorVisible;
		end_while;
		ui_flash_stop advisor_continue;



		;; Recruit town watch in Arretium
		monitor_event ItemDeselected TrueCondition;
			if I_CompareCounter DeselectActionComplete = 1;
				set_counter DeselectActionComplete 0;
				terminate_monitor;
			end_if;
			disable_ui open_recruitement_options;
			goto labelFive;
		end_monitor;
		advance_advice_thread Settlements_And_Garrisons_1_type_b;
		set_label labelFive;
		point_at_settlement Arretium, circle;
		monitor_event SettlementSelected SettlementName Arretium;
			if I_CompareCounter DeselectActionComplete = 1;
				terminate_monitor;
			end_if;
			set_counter ArretiumSelected 1;
		end_monitor;
		while I_CompareCounter ArretiumSelected = 0;
		end_while;
		set_counter ArretiumSelected 0;
		campaign_wait 0.2;
		stop_all_point_at_indicators;
		campaign_wait 2;
		enable_ui open_recruitement_options;
		ui_flash_start open_recruitement_options circle;
		campaign_wait 0.2;
		monitor_event RecruitmentPopulated TrueCondition;
			ui_flash_stop open_recruitement_options;
			point_at_card recruitment, roman city militia, square;
			disable_all_ui_cards recruitment;
			advance_completed_tasks 2;
			enable_ui_card recruitment, roman city militia;
		end_monitor;
		monitor_event ScrollOpened ScrollOpened unit_info_scroll;
			if I_CompareCounter DeselectActionComplete = 1;
                terminate_monitor;
            end_if;
            advance_completed_tasks 3;
		end_monitor;
		monitor_event RecruitmentItemClicked RecruitmentItemClicked roman city militia;
			stop_all_point_at_indicators;
			set_counter MilitiaSelected 1;
			disable_all_ui_cards recruitment;
			terminate_monitor;
		end_monitor;
		while I_CompareCounter MilitiaSelected = 0;
		end_while;
		set_counter MilitiaSelected 0;
		advance_completed_tasks 4;
		disable_all_ui_cards recruitment;
		disable_ui open_recruitement_options;
		ui_flash_stop open_recruitement_options;
		set_counter DeselectActionComplete 1;
		force_deselect_trigger;	
		campaign_wait 1;
		deselect_current_selection;
		strat_selection_unblocker settlement off Arretium;
		inhibit_camera_input true;
		disable_cursor;
		campaign_wait 0.1;
		move_strat_camera 87, 90;
		zoom_strat_camera 0.4;
		campaign_wait 0.1;
		enable_cursor;
		inhibit_camera_input false;
		campaign_wait 1;

		

		advance_advice_thread Send_Agent_Panel_1_type_e;
		character_flash_start Decius Curtius;
		set_counter AdviceAudioPlaying 0;
		declare_counter destroy_monitor;
		campaign_wait 1;
    	monitor_event AdvisorAudioStopped AdvisorAudioStopped;
    		if I_CompareCounter destroy_monitor = 1;
    			terminate_monitor;
    		end_if;
    		set_counter AdviceAudioPlaying 1;
    	end_monitor;
    	while I_CompareCounter AdviceAudioPlaying = 0;
    		if_not I_AdvisorSpeechPlaying;
    			set_counter AdviceAudioPlaying 1;
    		end_if;
    	end_while;
    	set_counter AdviceAudioPlaying 0;
		character_flash_stop Decius Curtius;
		stop_all_point_at_indicators;
		campaign_wait 1;



		;; Send Spy to Mediolanium
		monitor_event ItemDeselected TrueCondition;
			if I_CompareCounter DeselectActionComplete = 1;
				set_counter DeselectActionComplete 0;
				terminate_monitor;
			end_if;
			point_at_settlement Mediolanium, circle;
			goto labelSix;
		end_monitor;
		deselect_current_selection;
		strat_selection_unblocker settlement on Mediolanium;
		set_advice_page 2;
		monitor_event EnemySettlementSelected SettlementName Mediolanium;
				advance_completed_tasks 2;
				set_counter MediolaniumSelected 1;
				if I_CompareCounter DeselectActionComplete = 1;
        		    terminate_monitor;
        		end_if;
		end_monitor;
		set_label labelSix;
		stop_all_point_at_indicators;
		disable_agent_hub_all Mission;
		point_at_settlement Mediolanium, circle;
		while I_CompareCounter MediolaniumSelected = 0;
		end_while;
		set_counter MediolaniumSelected 0;
		campaign_wait 0.2;
		stop_all_point_at_indicators;
		strat_selection_unblocker character off Decius Curtius;
		strat_selection_unblocker settlement on Mediolanium;
		disable_ui open_agent_hub_enemy_options;
		disable_ui cancel_mission_selection;
		enable_ui send_agent_button;
		ui_flash_start send_agent_button square, down, scale 1.1 0.5;
		monitor_event ButtonPressed ButtonPressed send_agent_button;
			if I_CompareCounter DeselectActionComplete = 1;
				terminate_monitor;
			end_if;
			stop_all_point_at_indicators;
			ui_flash_stop send_agent_button;
			advance_completed_tasks 3;
			set_counter SendAgentPressed 1;
		end_monitor;
		monitor_event AgentListPopulated TrueCondition;
			if I_CompareCounter DeselectActionComplete = 1;
				terminate_monitor;
			end_if;
			disable_agent_hub_all Agent;
			enable_agent_hub Agent, Decius Curtius;
			point_at_agent_hub Agent, Decius Curtius, square;
		end_monitor;
		while I_CompareCounter SendAgentPressed = 0;
		end_while;
		set_counter SendAgentPressed 0;
		disable_ui send_agent_button;
		point_at_agent_hub Agent, Decius Curtius, square;
		monitor_event AgentSelected CharacterName Decius Curtius;
			if I_CompareCounter DeselectActionComplete = 1;
				terminate_monitor;
			end_if;
			disable_agent_hub_all Agent;
			disable_agent_hub_all Mission;
			enable_agent_hub Agent, Decius Curtius;
			enable_agent_hub Mission, Mediolanum;
			point_at_agent_hub Mission, Mediolanum, square;
			advance_completed_tasks 4;
		end_monitor;
		while I_CompareCounter DeciusPressed = 0;
			if AgentSelected TrueCondition;
				set_counter DeciusPressed 1;
			end_if;
		end_while;
		set_counter DeciusPressed 0;
		disable_agent_hub_all Mission;
		while I_CompareCounter ListPopulated = 0;
			if MissionSelected TrueCondition;
				set_counter ListPopulated 1;
			end_if;
		end_while;
		monitor_event MissionSelected TrueCondition;
			disable_agent_hub_all Mission;
			enable_agent_hub Mission, Mediolanum;
			ui_flash_start send_agent_button square, down, scale 1.3 0.5;
			enable_ui send_agent_button;
			clear_restrict_strat_movement;
			restrict_strat_movement romans_julii, 90, 85;
			restrict_strat_movement romans_julii, 84, 92;
			if I_CompareCounter DeselectActionComplete = 1;
				terminate_monitor;
			end_if;
		end_monitor;
		while I_CharacterNameNearTile romans_julii, Decius Curtius, 0 90,85;
		end_while;
		stop_all_point_at_indicators;
		ui_flash_stop send_agent_button;
		while_not I_CharacterNameNearTile romans_julii, Decius Curtius, 0.0 84,92;
		end_while;
		set_counter DeselectActionComplete 1;
		advance_completed_tasks 5;
		force_settlement_tab buildings;
		force_deselect_trigger;
		disable_ui send_agent_button;
		ui_flash_stop confirm_agent_button;
		stop_all_point_at_indicators;
		strat_selection_unblocker character off Decius Curtius;
		strat_selection_unblocker settlement off Mediolanium;
		deselect_current_selection;



		;; Display settlement defences
		disable_ui advisor_continue;
		advance_advice_thread Send_Agent_Panel_3_type_d;
		strat_selection_unblocker settlement on Mediolanium;
		disable_ui hud_show_units_tab;
		disable_ui hud_show_agents_tab;
		disable_ui hud_show_passengers_tab;
		monitor_event ItemDeselected TrueCondition;
			if I_CompareCounter DeselectActionComplete = 1;
				set_counter DeselectActionComplete 0;
				terminate_monitor;
			end_if;
			ui_flash_stop hud_show_buildings_tab;
			disable_ui hud_show_buildings_tab;
			stop_all_point_at_indicators;
			goto labelMedBuildings1;
		end_monitor;
		set_counter MediolaniumSelected 1;
		set_counter	BuildingsTabPressed 1;
		set_label labelMedBuildings1;
		point_at_settlement Mediolanium, circle;
		monitor_event EnemySettlementSelected SettlementName Mediolanium;
			set_counter MediolaniumSelected 1;
			if I_CompareCounter DeselectActionComplete = 1;
        	    terminate_monitor;
        	end_if;
		end_monitor;
		while I_CompareCounter MediolaniumSelected = 0;
		end_while;
		set_counter MediolaniumSelected 0;
		stop_all_point_at_indicators;
		enable_ui hud_show_buildings_tab;
		ui_flash_start hud_show_buildings_tab;
		monitor_event ButtonPressed ButtonPressed hud_show_buildings_tab;
			if I_CompareCounter DeselectActionComplete = 1;
                terminate_monitor;
            end_if;
			set_counter	BuildingsTabPressed 1;
			terminate_monitor;
		end_monitor;
		while I_CompareCounter BuildingsTabPressed = 0;
		end_while;
		set_counter	BuildingsTabPressed 0;
		ui_flash_stop hud_show_buildings_tab;
		set_counter DeselectActionComplete 1;
        force_deselect_trigger;



		monitor_event ItemDeselected TrueCondition;
			if I_CompareCounter DeselectActionComplete = 1;
				set_counter DeselectActionComplete 0;
				terminate_monitor;
			end_if;
			stop_all_point_at_indicators;
			disable_ui hud_show_buildings_tab;
			ui_flash_stop hud_show_buildings_tab;
			set_counter	BuildingsTabPressed 1;
			goto labelMedBuildings2;
		end_monitor;
		set_counter MediolaniumSelected 1;
		set_label labelMedBuildings2;
		point_at_settlement Mediolanium, circle;
			monitor_event EnemySettlementSelected SettlementName Mediolanium;

				set_counter MediolaniumSelected 1;

				if I_CompareCounter DeselectActionComplete = 1;
        		    terminate_monitor;
        		end_if;
			end_monitor;
		while I_CompareCounter MediolaniumSelected = 0;
		end_while;
		set_counter MediolaniumSelected 0;
		stop_all_point_at_indicators;
		enable_ui hud_show_buildings_tab;
        campaign_wait 1.5;
        enable_ui settlement_building_ui_cards;
        point_at_card building governors_villa, 0, square, blue 1;
        campaign_wait 2;
        point_at_card building wooden_pallisade, 0, square, blue 1;
        campaign_wait 2;
        point_at_card building muster_field, 0, square, blue 1;
        campaign_wait 2;
        stop_all_point_at_indicators;
        set_counter DeselectActionComplete 1;
        force_deselect_trigger;



		monitor_event ItemDeselected TrueCondition;
			if I_CompareCounter DeselectActionComplete = 1;
				set_counter DeselectActionComplete 0;
				terminate_monitor;
			end_if;
			stop_all_point_at_indicators;
			disable_ui hud_show_buildings_tab;
			disable_ui hud_show_units_tab;
			ui_flash_stop hud_show_units_tab;
			goto labelMedUnits1;
		end_monitor;
		set_counter MediolaniumSelected 1;
		set_label labelMedUnits1;
		point_at_settlement Mediolanium;
		monitor_event EnemySettlementSelected SettlementName Mediolanium;
			set_counter MediolaniumSelected 1;
			if I_CompareCounter DeselectActionComplete = 1;
        	    terminate_monitor;
        	end_if;
		end_monitor;
		while I_CompareCounter MediolaniumSelected = 0;
		end_while;
		set_counter MediolaniumSelected 0;
		stop_all_point_at_indicators;
		enable_ui hud_show_units_tab;
		ui_flash_start hud_show_units_tab circle;
		monitor_event ButtonPressed ButtonPressed hud_show_units_tab;
			if I_CompareCounter DeselectActionComplete = 1;
                terminate_monitor;
            end_if;
			set_counter	UnitsTabPressed 1;
			terminate_monitor;
		end_monitor;
		while I_CompareCounter UnitsTabPressed = 0;
		end_while;
		set_counter	UnitsTabPressed 0;
		ui_flash_stop hud_show_units_tab;
		disable_ui hud_show_buildings_tab;
		set_counter DeselectActionComplete 1;
        force_deselect_trigger;
        monitor_event ItemDeselected TrueCondition;
			if I_CompareCounter DeselectActionComplete = 1;
				set_counter DeselectActionComplete 0;
				terminate_monitor;
			end_if;
			stop_all_point_at_indicators;
			disable_ui hud_show_buildings_tab;
			disable_ui hud_show_units_tab;
			ui_flash_stop hud_show_units_tab;
			set_counter	UnitsTabPressed 1;
			goto labelMedUnits2;
		end_monitor;
		set_counter MediolaniumSelected 1;
		set_label labelMedUnits2;
		point_at_settlement Mediolanium, circle;
			monitor_event EnemySettlementSelected SettlementName Mediolanium;
				set_counter MediolaniumSelected 1;
				if I_CompareCounter DeselectActionComplete = 1;
        		    terminate_monitor;
        		end_if;
			end_monitor;
		while I_CompareCounter MediolaniumSelected = 0;
		end_while;
		set_counter MediolaniumSelected 0;
		stop_all_point_at_indicators;
		enable_ui hud_show_units_tab;
		enable_ui settlement_unit_ui_cards;
		ui_flash_stop hud_show_units_tab;
		disable_ui list_tooltip;
		point_at_card unit, slot 0, barb chieftain cavalry early gaul, 0, square, blue 1;				
		campaign_wait 2;
		stop_all_point_at_indicators;
		enable_ui list_tooltip;																		
		point_at_card unit, slot 1, barb infantry gaul, 0, square, blue 1;
		campaign_wait 2;
		stop_all_point_at_indicators;
		point_at_card unit, slot 2, barb infantry gaul, 1, square, blue 1;
		campaign_wait 2;
		stop_all_point_at_indicators;
		point_at_card unit, slot 3, barb cavalry gaul, 0, square, blue 1;
		campaign_wait 2;
		stop_all_point_at_indicators;
		set_counter DeselectActionComplete 1;
		force_deselect_trigger;



		campaign_wait 1;
		enable_ui hud_show_buildings_tab;
		enable_ui advisor_continue;
		ui_flash_start advisor_continue circle;
		while I_AdvisorVisible;
		end_while;
		ui_flash_stop advisor_continue;
		disable_ui advisor_continue;
		disable_ui show_senate_mission_tab;
		disable_ui show_news_tab;
		disable_ui show_alerts_tab;
		disable_ui show_reports_tab;
		enable_ui type_e_advice_left_arrow;
        enable_ui type_e_advice_right_arrow;
		campaign_wait 1.5;
		deselect_current_selection;
		ui_flash_stop hud_show_buildings_tab;
		strat_selection_unblocker settlement off Mediolanium;
		disable_ui hud_show_units_tab;
		disable_ui hud_show_buildings_tab;
		campaign_wait 1.5;



		;; Use of Diplomats
		advance_advice_thread Use_Of_Diplomats_1_type_e;
		campaign_wait 1;
		set_strat_camera_speed 24;
		inhibit_camera_input true;
		disable_cursor;
		campaign_wait 0.5;
		move_strat_camera 108, 54;
		campaign_wait 0.5;
		enable_cursor;
		inhibit_camera_input false;
		set_strat_camera_speed 6;
		while I_AdvisorSpeechPlaying;
		end_while;
		campaign_wait 1;
		set_advice_page 2;
		enable_ui advisor_continue_type_e;
		while I_AdvisorSpeechPlaying;
		end_while;
    	ui_flash_start advisor_continue_type_e circle;
		while I_AdvisorVisible;
		end_while;
		ui_flash_stop advisor_continue_type_e;




		;; Embark on a ship and move the ship towards Caralis
		monitor_event ItemDeselected TrueCondition;
            if I_CompareCounter DeselectActionComplete = 1;
                set_counter DeselectActionComplete 0;
                terminate_monitor;
            end_if;
            stop_all_point_at_indicators;
            goto labelFifteen;
        end_monitor;
		clear_restrict_strat_movement;
		restrict_strat_movement romans_julii, 107, 54;
		advance_advice_thread Embark_Unit_1_type_b;
		campaign_wait 2;
		set_label labelFifteen;
		disable_entire_ui;					
		enable_ui post_battle_scroll;
		enable_ui post_battle_stats_show_results_button;
		disable_all_ui_cards unit;
		disable_all_ui_cards building;
		disable_ui agent_confirm_button;
		enable_ui campaign_map_pause_button;
		enable_ui advisor_continue;
		enable_ui type_c_advisor_highlight;
		enable_ui type_c_advisor_button;
		enable_ui type_c_advice_left_arrow;
        enable_ui type_c_advice_right_arrow;
        enable_ui type_e_advice_left_arrow;
        enable_ui type_e_advice_right_arrow;
		enable_ui wonder_close_button;
		enable_ui tooltip;
		enable_ui stop_tutorial_button;

		point_at_character slot 0, Sextus Antio, circle;
		strat_selection_unblocker character on Sextus Antio;
		while_not I_CharacterSelected Sextus Antio;
		end_while;
		stop_all_point_at_indicators;
		advance_completed_tasks 2;
		campaign_wait 1;
		point_at_strat_position_alt, slot 1, 107, 54, y_offset -10;
		while_not I_CharacterTypeNearTile romans_julii diplomat, 0.0 107, 54;
		end_while;
		set_counter DeselectActionComplete 1;
        force_deselect_trigger;
        advance_completed_tasks 3;
		stop_all_point_at_indicators;
		campaign_wait 1;



		;; Explain Navy
		monitor_event ItemDeselected TrueCondition;
            if I_CompareCounter DeselectActionComplete = 1;
                set_counter DeselectActionComplete 0;
                terminate_monitor;
            end_if;
            stop_all_point_at_indicators;
            goto labelSixteen;
        end_monitor;
 		advance_advice_thread Navy_1_type_b;
		inhibit_camera_input true;
		campaign_wait 1;
		move_strat_camera 95, 49;
		zoom_strat_camera 0.1;
		campaign_wait 1;
		inhibit_camera_input false;
        set_label labelSixteen;
		point_at_character slot 0, Oppius Clausus, circle;
		strat_selection_unblocker character on Admiral Oppius;
		while_not I_CharacterSelected Oppius Clausus;
		end_while;
		advance_completed_tasks 2;
		stop_all_point_at_indicators;
		campaign_wait 1;
		clear_restrict_strat_movement;
		restrict_strat_movement romans_julii, 94, 51;
		point_at_strat_position_alt, slot 1, 94, 51, y_offset 10;												;;Fleet movement towards Sicily
		while_not I_CharacterTypeNearTile romans_julii admiral, 1.45 94,51;
		end_while;
		clear_restrict_strat_movement;
		restrict_strat_movement romans_julii, 95, 50;
		set_counter DeselectActionComplete 1;
        force_deselect_trigger;
        advance_completed_tasks 3;
		stop_all_point_at_indicators;
		campaign_wait 2;



		;; Move ship with diplomat towards Lilybaeum
		monitor_event ItemDeselected TrueCondition;
            if I_CompareCounter DeselectActionComplete = 1;
                set_counter DeselectActionComplete 0;
                terminate_monitor;
            end_if;
            stop_all_point_at_indicators;
            goto labelTwentyone;
        end_monitor;
		clear_strat_selection_unblocker;
		strat_selection_unblocker character on Admiral Oppius;
		strat_selection_unblocker character on Sextus Antio;
		advance_advice_thread Diplomacy_1_type_b;
		campaign_wait 2;
		inhibit_camera_input true;
		campaign_wait 0.5;
		zoom_strat_camera 0.4;
		campaign_wait 0.5;
		inhibit_camera_input false;
		set_label labelTwentyone;
		set_counter DeselectActionComplete 1;
        force_deselect_trigger;
		campaign_wait 1;
		monitor_event ItemDeselected TrueCondition;
            if I_CompareCounter DeselectActionComplete = 1;
                set_counter DeselectActionComplete 0;
               terminate_monitor;
            end_if;
            goto labelTwentytwo;
        end_monitor;
        set_label labelTwentytwo;
        stop_all_point_at_indicators;
		point_at_character slot 0, Oppius Clausus, circle;
		while_not I_CharacterSelected Oppius Clausus;
		end_while;
		stop_all_point_at_indicators;
		point_at_strat_position_alt, slot 1, 95, 50, y_offset 10;											;;Disembarking the diplomat
		while_not I_CharacterTypeNearTile romans_julii diplomat, 0.0 95,50;
		end_while;
		advance_completed_tasks 2;
		replenish_action_points Decius Curtius;
		set_counter DeselectActionComplete 1;
        force_deselect_trigger;
		stop_all_point_at_indicators;
		strat_selection_unblocker character off Admiral Oppius;
		campaign_wait 1;
		monitor_event DiplomacyScrollPopulated TrueCondition;
			disable_ui diplomacy_item_trade_rights;
			disable_ui diplomacy_item_alliance;
			disable_ui diplomacy_item_attack_faction;
			disable_ui diplomacy_item_demand_attack_faction;
			disable_ui diplomacy_item_cede_region;
			disable_ui diplomacy_item_demand_cede_region;
			disable_ui diplomacy_item_single_payment;
			disable_ui diplomacy_item_demand_single_payment;
			disable_ui diplomacy_item_yearly_payment;
			disable_ui diplomacy_item_demand_yearly_payment;
			disable_ui diplomacy_item_map_information;
			disable_ui diplomacy_item_demand_map_information;
			disable_ui diplomacy_offer_button;
			disable_ui diplomacy_accept_offer_button;
			disable_ui diplomacy_decline_offer_button;
			disable_ui diplomacy_item_threat_of_attack;
			disable_ui diplomacy_item_bribe;
			disable_ui diplomacy_end_negotiations_button;
			disable_ui diplomacy_item_embargo;
			if I_CompareCounter MapDemandPressed = 1;
				if I_CompareCounter DeselectActionComplete = 1;
                	terminate_monitor;
            	end_if;
			end_if;
			set_counter DeselectActionComplete 1;
        	force_deselect_trigger;
		end_monitor;
		deselect_current_selection;
		campaign_wait 1;
		advance_advice_thread Diplomacy_2_type_b;
		campaign_wait 3;
		monitor_event ItemDeselected TrueCondition;
            if I_CompareCounter DeselectActionComplete = 1;
                set_counter DeselectActionComplete 0;
                terminate_monitor;
            end_if;
            stop_all_point_at_indicators;
            goto labelTwentytwotwo;
        end_monitor;
        set_label labelTwentytwotwo;
		point_at_character slot 0, Sextus Antio, circle;
		while_not I_CharacterSelected Sextus Antio;
		end_while;
		advance_completed_tasks 2;
		stop_all_point_at_indicators;
		clear_restrict_strat_movement;
		restrict_strat_movement romans_julii, 95, 50;
		restrict_strat_movement romans_julii, 94, 48;
		point_at_strat_position, slot 1, 94, 48, y_offset -60;
		monitor_event ScrollOpened ScrollOpened diplomacy_scroll;
			if I_CompareCounter DeselectActionComplete = 1;
                terminate_monitor;
            end_if;
            set_counter DiplomacyStarted 1;
            set_counter DeselectActionComplete 1;
        	force_deselect_trigger;
		end_monitor;
		while I_CompareCounter DiplomacyStarted = 0;
		end_while;
		advance_completed_tasks 3;
		stop_all_point_at_indicators;

		monitor_event ButtonPressed ButtonPressed type_e_advice_right_arrow;
			disable_ui type_e_advice_left_arrow;
        	disable_ui type_e_advice_right_arrow;	
			terminate_monitor;
		end_monitor;
		
		;; JNF 2021-04-12 - Disable diplomacy barks for this section of the tutorial
		enable_diplomacy_voices off;

		campaign_wait 1;
		advance_advice_thread Diplomacy_3_type_e;
		declare_counter destroy_monitor;
		set_counter AdviceAudioPlaying 0;
		campaign_wait 1;
    	monitor_event AdvisorAudioStopped AdvisorAudioStopped;
    		if I_CompareCounter destroy_monitor = 1;
    			terminate_monitor;
    		end_if;
    		set_counter AdviceAudioPlaying 1;
    	end_monitor;
    	
    	while I_CompareCounter AdviceAudioPlaying = 0;
    		if_not I_AdvisorSpeechPlaying;
    			set_counter AdviceAudioPlaying 1;
    		end_if;
    	end_while;
    	set_counter AdviceAudioPlaying 0;
		campaign_wait 1;

		set_counter CloseDialogPressed 0;

		;; Restart diplomacy section if pause menu is opened (cmd + q)
		monitor_event ScrollClosed ScrollClosed pause_menu;
			if I_CompareCounter MakeOfferPressed = 1;
				goto end_diplomacy;
			end_if;
			goto begin_diplomacy;
			if I_CompareCounter CloseDialogPressed = 1;
				terminate_monitor;
			end_if;
		end_monitor;

		set_label begin_diplomacy;
		set_advice_page 2;
		disable_ui type_e_advice_left_arrow;
		disable_ui type_e_advice_right_arrow;
		disable_diplomacy_ui;
		stop_all_point_at_indicators;

		set_counter	CloseDialogPressed 0;
		set_counter	TradeOfferPressed 0;
		set_counter	MapOfferPressed 0;
		set_counter	MapDemandPressed 0;
		set_counter	MakeOfferPressed 0;
		campaign_wait 3;



		;; Offer trade right
		monitor_event ButtonPressed ButtonPressed diplomacy_item_trade_rights;
			set_counter	TradeOfferPressed 1;
			if I_CompareCounter CloseDialogPressed = 1;
				terminate_monitor;
			end_if;
		end_monitor;

		disable_diplomacy_ui;
		enable_ui diplomacy_item_trade_rights;
		point_at_diplomacy_offer, diplomacy_item_trade_rights, square, scale 1.1 0.4;
		while I_CompareCounter TradeOfferPressed = 0;
		end_while;
		ui_flash_stop diplomacy_item_trade_rights;
		advance_completed_tasks 2;



		;; Offer map information
		monitor_event ButtonPressed ButtonPressed diplomacy_item_map_information;
			set_counter	MapOfferPressed 1;
			if I_CompareCounter CloseDialogPressed = 1;
				terminate_monitor;
			end_if;
		end_monitor;

		disable_diplomacy_ui;
		enable_ui diplomacy_item_map_information;
		point_at_diplomacy_offer, diplomacy_item_map_information, square, scale 1.1 0.4;
		while I_CompareCounter MapOfferPressed = 0;
		end_while;
		ui_flash_stop diplomacy_item_map_information;



		;; Demand map information
		monitor_event ButtonPressed ButtonPressed diplomacy_item_demand_map_information;
			set_counter	MapDemandPressed 1;
			if I_CompareCounter CloseDialogPressed = 1;
				terminate_monitor;
			end_if;
		end_monitor;

		disable_diplomacy_ui;
		enable_ui diplomacy_item_demand_map_information;
		point_at_diplomacy_offer, diplomacy_item_demand_map_information, square, scale 1.1 0.4;
		while I_CompareCounter MapDemandPressed = 0;
		end_while;
		ui_flash_stop diplomacy_item_demand_map_information;
		set_advice_page 3;



		;; Offer button presss
		monitor_event ButtonPressed ButtonPressed diplomacy_offer_button;
			ui_flash_start diplomacy_offer_button circle;
			set_counter	MakeOfferPressed 1;
			if I_CompareCounter CloseDialogPressed = 1;
				terminate_monitor;
			end_if;
		end_monitor;

		disable_diplomacy_ui;
		enable_ui diplomacy_offer_button;
		ui_flash_start diplomacy_offer_button circle;
		while I_CompareCounter MakeOfferPressed = 0;
		end_while;
		advance_completed_tasks 2;
		ui_flash_stop diplomacy_offer_button;
		disable_ui diplomacy_offer_button;

		set_advice_page 4;
		enable_ui type_e_advice_left_arrow;
        enable_ui type_e_advice_right_arrow;

		while I_AdvisorSpeechPlaying;
		end_while;
		campaign_wait 2;
		set_advice_page 5;	
		campaign_wait 1;


		;; End negotiations
		zoom_strat_camera 0.5;
		set_label end_diplomacy;
		;; JNF 2021-04-12 - Re-enable the diplomacy voices
		enable_diplomacy_voices on;

		monitor_event ButtonPressed ButtonPressed diplomacy_end_negotiations_button;
			set_counter	CloseDialogPressed 1;
			if I_CompareCounter CloseDialogPressed = 1;
                terminate_monitor;
            end_if;
		end_monitor;

		disable_diplomacy_ui;
		enable_ui diplomacy_end_negotiations_button;
		ui_flash_start diplomacy_end_negotiations_button circle;
		campaign_wait 0.1;

		while I_CompareCounter CloseDialogPressed = 0;
		end_while;
		advance_completed_tasks 2;
		ui_flash_stop diplomacy_end_negotiations_button;
		deselect_current_selection;
		strat_selection_unblocker character off Sextus Antio;
		campaign_wait 3;
		enable_ui advisor_continue_type_e;


		;;Explain End turn
		advance_advice_thread End_Turn_1;
		campaign_wait 0.5;
		monitor_event ButtonPressed ButtonPressed end_turn;
			set_counter	PressEndTurn 1;
			if I_CompareCounter DeselectActionComplete = 1;
                terminate_monitor;
            end_if;
		end_monitor;
		enable_ui advisor_continue;
		while I_AdvisorSpeechPlaying;
		end_while;
		ui_flash_start advisor_continue circle;
		while I_AdvisorVisible;
		end_while;
		ui_flash_stop advisor_continue;
		enable_ui end_turn;
		ui_flash_start end_turn circle;
		while I_CompareCounter PressEndTurn = 0;
		end_while;
		disable_ui stop_tutorial_button;

		ui_flash_stop end_turn;
		enable_entire_ui;
		set_strat_camera_speed 15;
		stop_all_point_at_indicators;
		suspend_unscripted_advice false;
		inhibit_camera_input false;
		disable_shortcuts false;
		clear_restrict_strat_movement;
		clear_strat_selection_unblocker;
		set_marriage_allowed on;
		force_autoresolve_outcome no;
		force_agent_succeed no;
		force_diplomacy accept, no;
		ai_active_set on;
		terminate_prologue end_of_tutorial;
		terminate_script;